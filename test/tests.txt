Utterance: show me all museums
SPARQL: 
SELECT DISTINCT ?x WHERE { 
    ?x <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q207694>.
}
TT: 
@org.wikidata . art_museum ( ) ;

====
Utterance: show me museums named after louvre palace
SPARQL: 
SELECT DISTINCT ?x WHERE { 
    ?x <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q207694>;
       <http://www.wikidata.org/prop/direct/P138> <http://www.wikidata.org/entity/Q1075988>.
}
TT: 
@org.wikidata . art_museum ( ) filter contains ( named_after , " Q1075988 " ^^org.wikidata:p_named_after ( " louvre palace " ) ) ;

====
Utterance: show me the owner of museums named after louvre palace
SPARQL: 
SELECT DISTINCT ?y WHERE { 
    ?x <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q207694>;
       <http://www.wikidata.org/prop/direct/P138> <http://www.wikidata.org/entity/Q1075988>.
    ?x <http://www.wikidata.org/prop/direct/P127> ?y.
}
TT: 
[ owned_by ] of @org.wikidata . art_museum ( ) filter contains ( named_after , " Q1075988 " ^^org.wikidata:p_named_after ( " louvre palace " ) ) ;

====
Utterance: Who was the doctoral supervisor of Albert Einstein
SPARQL: 
SELECT DISTINCT ?uri WHERE { 
    <http://www.wikidata.org/entity/Q937> <http://www.wikidata.org/prop/direct/P184> ?uri
}
TT:
[ doctoral_advisor ] of @org.wikidata . human ( ) filter id == " Q937 " ^^org.wikidata:human ( " albert einstein " ) ;

====
Utterance: Did Kaurismäki ever win the Grand Prix at Cannes?
SPARQL:
ASK WHERE { 
    <http://www.wikidata.org/entity/Q276186> <http://www.wikidata.org/prop/direct/P166>  <http://www.wikidata.org/entity/Q844804> . 
}
TT: 
[ contains ( award_received , " Q844804 " ^^org.wikidata:p_award_received ( " grand prix at cannes " ) ) ] of @org.wikidata . human ( ) filter id == " Q276186 " ^^org.wikidata:human ( " kaurismäki " ) ;

====
Utterance: Which countries have more than two official languages??
SPARQL: 
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q6256> . 
    ?uri <http://www.wikidata.org/prop/direct/P37> ?language . 
} GROUP BY ?uri HAVING(COUNT(?language)>2)
TT: 
@org.wikidata . country ( ) filter count ( official_language ) >= 2 ;

====
Utterance: What is the highest mountain in Australia?
SPARQL:
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q8502> . 
    ?uri <http://www.wikidata.org/prop/direct/P30> <http://www.wikidata.org/entity/Q3960> . 
    ?uri <http://www.wikidata.org/prop/direct/P2044> ?height. 
} ORDER BY DESC(?height) LIMIT 1
TT: 
sort ( elevation_above_sea_level desc of @org.wikidata . mountain ( ) filter contains ( continent , " Q3960 " ^^org.wikidata:p_continent ( " australia " ) ) ) [ 1 ] ;

====
Utterance: Give me all cities in New Jersey with more than 10000 inhabitants.
SPARQL:
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q515> . 
    ?uri <http://www.wikidata.org/prop/direct/P131>+ <http://www.wikidata.org/entity/Q1408> . 
    ?uri <http://www.wikidata.org/prop/direct/P1082> ?population . 
    FILTER(?population > 10000) 
}
TT:
@org.wikidata . city ( ) filter contains ( located_in_the_administrative_territorial_entity , " Q1408 " ^^org.wikidata:p_located_in_the_administrative_territorial_entity ( " new jersey " ) ) && population >= 10000 ;

====
Utterance: Whose wife is called Mary?
SPARQL: 
SELECT DISTINCT ?uri { 
    ?uri <http://www.wikidata.org/prop/direct/P26> ?spouse .  
    ?spouse <http://www.w3.org/2000/01/rdf-schema#label> ?name .  
    FILTER(regex(?name,'Mary')) 
}
TT: 
@org.wikidata . entity ( ) filter contains~ ( spouse , " mary " ) ;

====
Utterance: Was the wife of president Lincoln called Mary?
SPARQL: 
ASK WHERE { 
    <http://www.wikidata.org/entity/Q91> <http://www.wikidata.org/prop/direct/P26> ?spouse .  
    ?spouse <http://www.w3.org/2000/01/rdf-schema#label> ?name .  
    FILTER(regex(?name,'Mary')) 
}
TT: 
[ contains~ ( spouse , " mary " ) ] of @org.wikidata . human ( ) filter id == " Q91 " ^^org.wikidata:human ( " lincoln " ) ;

====
Utterance: How many countries are there in Europe ? 
SPARQL: 
SELECT (COUNT(DISTINCT ?uri) as ?c) WHERE {
     ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q6256> . 
     ?uri <http://www.wikidata.org/prop/direct/P30> <http://www.wikidata.org/entity/Q46> . 
}
TT: 
count ( @org.wikidata . country ( ) filter contains ( continent , " Q46 " ^^org.wikidata:p_continent ( " europe " ) ) ) ;

====
Utterance: Which actors were born in Germany?
SPARQL:
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P106> <http://www.wikidata.org/entity/Q10800557> . 
    { ?uri <http://www.wikidata.org/prop/direct/P19> <http://www.wikidata.org/entity/Q183> . } UNION 
    { ?uri <http://www.wikidata.org/prop/direct/P19>/<http://www.wikidata.org/prop/direct/P17> <http://www.wikidata.org/entity/Q183> . } 
}
TT: 
@org.wikidata . entity ( ) filter contains ( occupation , " Q10800557 " ^^org.wikidata:p_occupation ( " actors " ) ) && contains ( place_of_birth , " Q183 " ^^org.wikidata:p_place_of_birth ( " germany " ) ) ;

====
Utterance: Give me the birthdays of all actors of the television show Charmed.
SPARQL: 
SELECT DISTINCT ?date WHERE { 
    <http://www.wikidata.org/entity/Q162371> <http://www.wikidata.org/prop/direct/P161>/<http://www.wikidata.org/prop/direct/P569> ?date .
}
TT: 
[ < cast_member / date_of_birth > ] of @org.wikidata . television_series ( ) filter id == " Q162371 " ^^org.wikidata:television_series ( " charmed " ) ;

====
Utterance: Does the Isar flow into a lake?
SPARQL:
ASK WHERE { 
    <http://www.wikidata.org/entity/Q106588> <http://www.wikidata.org/prop/direct/P403> ?uri . 
    ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q23397>  . 
}
TT:
[ contains ( mouth_of_the_watercourse , any ( [ id ] of @org.wikidata . lake ( ) ) ) ] of @org.wikidata . river ( ) filter id == " Q106588 " ^^org.wikidata:river ( " isar " ) ;

====
Utterance: Who wrote the lyrics for the Polish national anthem?
SPARQL:
SELECT DISTINCT ?uri WHERE { 
    <http://www.wikidata.org/entity/Q36> <http://www.wikidata.org/prop/direct/P85>/<http://www.wikidata.org/prop/direct/P676> ?uri . 
}
TT:
[ < anthem / lyrics_by > ] of @org.wikidata . sovereign_state ( ) filter id == " Q36 " ^^org.wikidata:sovereign_state ( " polish " ) ;

====
Utterance: Which writers studied in Istanbul?
SPARQL:
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P106> <http://www.wikidata.org/entity/Q36180> . 
    ?uri <http://www.wikidata.org/prop/direct/P69>/<http://www.wikidata.org/prop/direct/P131>  <http://www.wikidata.org/entity/Q406> . 
}
TT:
@org.wikidata . entity ( ) filter contains ( < educated_at / located_in_the_administrative_territorial_entity > , " Q406 " ^^org.wikidata:p_located_in_the_administrative_territorial_entity ( " istanbul " ) ) && contains ( occupation , " Q36180 " ^^org.wikidata:p_occupation ( " writers " ) ) ;

====
Utterance: Which writers studied in Istanbul?
SPARQL:
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P106> <http://www.wikidata.org/entity/Q36180> . 
    ?uri <http://www.wikidata.org/prop/direct/P69>/<http://www.wikidata.org/prop/direct/P131>+  <http://www.wikidata.org/entity/Q406> . 
}
TT:
@org.wikidata . entity ( ) filter contains ( < educated_at / located_in_the_administrative_territorial_entity > , " Q406 " ^^org.wikidata:p_located_in_the_administrative_territorial_entity ( " istanbul " ) ) && contains ( occupation , " Q36180 " ^^org.wikidata:p_occupation ( " writers " ) ) ;

====
Utterance: In which city did John F. Kennedy die?
SPARQL:
SELECT DISTINCT ?uri WHERE {  
    <http://www.wikidata.org/entity/Q9696> <http://www.wikidata.org/prop/direct/P20>/<http://www.wikidata.org/prop/direct/P131> ?uri . 
    ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q515>  .  
} 
TT: 
[ < place_of_death / located_in_the_administrative_territorial_entity > : Entity ( org.wikidata:city ) ] of @org.wikidata . human ( ) filter id == " Q9696 " ^^org.wikidata:human ( " john f . kennedy " ) ;


====
Utterance: Which mountain is the highest after the Annapurna?
SPARQL: 
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q8502> . 
    ?uri <http://www.wikidata.org/prop/direct/P2044> ?elevation . 
    <http://www.wikidata.org/entity/Q16466024> <http://www.wikidata.org/prop/direct/P2044> ?elevation2 . 
    FILTER (?elevation < ?elevation2) . 
} ORDER BY DESC(?elevation) LIMIT 1
TT:
sort ( elevation_above_sea_level desc of @org.wikidata . mountain ( ) filter elevation_above_sea_level <= any ( [ elevation_above_sea_level ] of @org.wikidata . mountain ( ) filter id == " Q16466024 " ^^org.wikidata:mountain ( " annapurna " ) ) ) [ 1 ] ;

==== 
Utterance: Are tree frogs a type of amphibian?
SPARQL:
ASK WHERE { 
    <http://www.wikidata.org/entity/Q53696> <http://www.wikidata.org/prop/direct/P171>* <http://www.wikidata.org/entity/Q10908> . 
}
TT:
[ contains ( < parent_taxon * > , " Q10908 " ^^org.wikidata:p_parent_taxon ( " amphibian " ) ) ] of @org.wikidata . taxon ( ) filter id == " Q53696 " ^^org.wikidata:taxon ( " tree frog " ) ;

====
Utterance: Which telecommunications organizations are located in Belgium?
SPARQL: 
SELECT DISTINCT ?uri WHERE { 
    { ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q43229> } UNION 
    { ?uri <http://www.wikidata.org/prop/direct/P31>/(<http://www.wikidata.org/prop/direct/P279>*) <http://www.wikidata.org/entity/Q43229> } . 
    ?uri <http://www.wikidata.org/prop/direct/P452> <http://www.wikidata.org/entity/Q418> .  
    ?uri <http://www.wikidata.org/prop/direct/P17> <http://www.wikidata.org/entity/Q31> . 
}
TT: 
@org.wikidata . organization ( ) filter contains ( industry , " Q418 " ^^org.wikidata:p_industry ( " telecommunications " ) ) && contains ( country , " Q31 " ^^org.wikidata:p_country ( " belgium " ) ) ;

====
Utterance: Is there a video game called Battle Chess?
SPARQL: 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>  
ASK WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q7889> . 
    ?uri rdfs:label "Battle Chess"@en 
}
TT: 
[ count >= 1 ] of count ( @org.wikidata . video_game ( ) filter id =~ " battle chess " ) ;

====
Utterance: Give me the websites of companies with more than 500000 employees.
SPARQL:
SELECT DISTINCT ?string WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P31>/(<http://www.wikidata.org/prop/direct/P279>*) <http://www.wikidata.org/entity/Q4830453> . 
    ?uri <http://www.wikidata.org/prop/direct/P1128> ?number . 
    FILTER(?number > 500000) . 
    ?uri <http://www.wikidata.org/prop/direct/P856> ?string . 
}
TT:
[ official_website ] of @org.wikidata . business ( ) filter employees >= 500000 ;

====
Utterance: Is Lake Baikal bigger than the Great Bear Lake?
SPARQL:
ASK WHERE { 
    <http://www.wikidata.org/entity/Q5513> <http://www.wikidata.org/prop/direct/P2046> ?area1 . 
    <http://www.wikidata.org/entity/Q5525> <http://www.wikidata.org/prop/direct/P2046> ?area2 . 
    FILTER(?area1 > ?area2) 
}
TT: 
[ area >= any ( [ area ] of @org.wikidata . lake ( ) filter id == " Q5525 " ^^org.wikidata:lake ( " great bear lake " ) ) ] of @org.wikidata . lake ( ) filter id == " Q5513 " ^^org.wikidata:lake ( " lake baikal " ) ;

====
Utterance: Was U.S. president Jackson involved in a war?
SPARQL: 
ASK WHERE { 
    <http://www.wikidata.org/entity/Q11817> <http://www.wikidata.org/prop/direct/P607> ?uri . 
}
TT:
[ ! ( count ( conflict ) == 0 ) ] of @org.wikidata . human ( ) filter id == " Q11817 " ^^org.wikidata:human ( " jackson " ) ;

====
Utterance: How many people live in the capital of Australia?
SPARQL: 
SELECT DISTINCT ?number WHERE { 
    <http://www.wikidata.org/entity/Q408> <http://www.wikidata.org/prop/direct/P36> ?uri . 
    ?uri <http://www.wikidata.org/prop/direct/P1082> ?number . 
}
TT:
[ population ] of @org.wikidata . entity ( ) filter in_array ( id , any ( [ capital ] of @org.wikidata . sovereign_state ( ) filter id == " Q408 " ^^org.wikidata:sovereign_state ( " australia " ) ) ) ;

====
Utterance: Was the Cuban Missile Crisis earlier than the Bay of Pigs Invasion?
SPARQL:
ASK WHERE { 
    <http://www.wikidata.org/entity/Q128160> <http://www.wikidata.org/prop/direct/P582> ?date1 . 
    <http://www.wikidata.org/entity/Q191721> <http://www.wikidata.org/prop/direct/P580> ?date2 . 
    FILTER(?date1 < ?date2) 
}
TT:
[ end_time <= any ( [ start_time ] of @org.wikidata . landing_operation ( ) filter id == " Q191721 " ^^org.wikidata:landing_operation ( " bay of pigs invasion " ) ) ] of @org.wikidata . crisis ( ) filter id == " Q128160 " ^^org.wikidata:crisis ( " cuban missile crisis " ) ;
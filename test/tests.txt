Utterance: show me all museums
Keywords: 
SPARQL: 
SELECT DISTINCT ?x WHERE { 
    ?x <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q207694>.
}
TT: 
@org.wikidata . art_museum ( ) ;

====
Utterance: show me museums named after louvre palace
Keywords: louvre palace
SPARQL: 
SELECT DISTINCT ?x WHERE { 
    ?x <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q207694>;
       <http://www.wikidata.org/prop/direct/P138> <http://www.wikidata.org/entity/Q1075988>.
}
TT: 
@org.wikidata . art_museum ( ) filter contains ( named_after , " Q1075988 " ^^org.wikidata:p_named_after ( " louvre palace " ) ) ;

====
Utterance: show me the owner of museums named after louvre palace
Keywords: louvre palace
SPARQL: 
SELECT DISTINCT ?y WHERE { 
    ?x <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q207694>;
       <http://www.wikidata.org/prop/direct/P138> <http://www.wikidata.org/entity/Q1075988>.
    ?x <http://www.wikidata.org/prop/direct/P127> ?y.
}
TT: 
[ owned_by ] of @org.wikidata . art_museum ( ) filter contains ( named_after , " Q1075988 " ^^org.wikidata:p_named_after ( " louvre palace " ) ) ;

====
Utterance: Who was the doctoral supervisor of Albert Einstein
Keywords: doctoral supervisor, Albert Einstein
SPARQL: 
SELECT DISTINCT ?uri WHERE { 
    <http://www.wikidata.org/entity/Q937> <http://www.wikidata.org/prop/direct/P184> ?uri
}
TT:
[ doctoral_advisor ] of @org.wikidata . entity ( ) filter id == " Q937 " ^^org.wikidata:entity ( " albert einstein " ) ;

====
Utterance: Did Kaurismäki ever win the Grand Prix at Cannes?
Keywords: Kaurismäki, winner, Grand Prix at Cannes
SPARQL:
ASK WHERE { 
    <http://www.wikidata.org/entity/Q276186> <http://www.wikidata.org/prop/direct/P166>  <http://www.wikidata.org/entity/Q844804> . 
}
TT: 
[ contains ( award_received , " Q844804 " ^^org.wikidata:p_award_received ( " grand prix at cannes " ) ) ] of @org.wikidata . entity ( ) filter id == " Q276186 " ^^org.wikidata:entity ( " kaurismäki " ) ;

====
Utterance: Which countries have more than two official languages??
Keywords: country, official language, more than two
SPARQL: 
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q6256> . 
    ?uri <http://www.wikidata.org/prop/direct/P37> ?language . 
} GROUP BY ?uri HAVING(COUNT(?language)>2)
TT: 
@org.wikidata . country ( ) filter count ( official_language ) >= 2 ;

====
Utterance: What is the highest mountain in Australia?
Keywords: Australia, highest mountain
SPARQL:
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q8502> . 
    ?uri <http://www.wikidata.org/prop/direct/P30> <http://www.wikidata.org/entity/Q3960> . 
    ?uri <http://www.wikidata.org/prop/direct/P2044> ?height. 
} ORDER BY DESC(?height) LIMIT 1
TT: 
sort ( elevation_above_sea_level desc of @org.wikidata . mountain ( ) filter contains ( continent , " Q3960 " ^^org.wikidata:p_continent ( " australia " ) ) ) [ 1 ] ;

====
Utterance: Give me all cities in New Jersey with more than 10000 inhabitants.
Keywords: New Jersey, city, inhabitants, more than 10000
SPARQL:
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q515> . 
    ?uri <http://www.wikidata.org/prop/direct/P131>+ <http://www.wikidata.org/entity/Q1408> . 
    ?uri <http://www.wikidata.org/prop/direct/P1082> ?population . 
    FILTER(?population > 10000) 
}
TT:
@org.wikidata . city ( ) filter contains ( located_in_the_administrative_territorial_entity , " Q1408 " ^^org.wikidata:p_located_in_the_administrative_territorial_entity ( " new jersey " ) ) && population >= 10000 ;

====
Utterance: Whose wife is called Mary?
Keywords: wife, called, Mary
SPARQL: 
SELECT DISTINCT ?uri { 
    ?uri <http://www.wikidata.org/prop/direct/P26> ?spouse .  
    ?spouse <http://www.w3.org/2000/01/rdf-schema#label> ?name .  
    FILTER(regex(?name,'Mary')) 
}
TT: 
@org.wikidata . entity ( ) filter contains~ ( spouse , " mary " ) ;

====
Utterance: Was the wife of president Lincoln called Mary?
Keywords: Lincoln, president, wife, called, Mary   
SPARQL: 
ASK WHERE { 
    <http://www.wikidata.org/entity/Q91> <http://www.wikidata.org/prop/direct/P26> ?spouse .  
    ?spouse <http://www.w3.org/2000/01/rdf-schema#label> ?name .  
    FILTER(regex(?name,'Mary')) 
}
TT: 
[ contains~ ( spouse , " mary " ) ] of @org.wikidata . entity ( ) filter id == " Q91 " ^^org.wikidata:entity ( " lincoln " ) ;

====
Utterance: How many countries are there in Europe ? 
Keywords: Europe, countries
SPARQL: 
SELECT (COUNT(DISTINCT ?uri) as ?c) WHERE {
     ?uri <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q6256> . 
     ?uri <http://www.wikidata.org/prop/direct/P30> <http://www.wikidata.org/entity/Q46> . 
}
TT: 
count ( @org.wikidata . country ( ) filter contains ( continent , " Q46 " ^^org.wikidata:p_continent ( " europe " ) ) ) ;

====
Utterance: Which actors were born in Germany?
Keywords: actor, born, Germany
SPARQL:
SELECT DISTINCT ?uri WHERE { 
    ?uri <http://www.wikidata.org/prop/direct/P106> <http://www.wikidata.org/entity/Q10800557> . 
    { ?uri <http://www.wikidata.org/prop/direct/P19> <http://www.wikidata.org/entity/Q183> . } UNION 
    { ?uri <http://www.wikidata.org/prop/direct/P19>/<http://www.wikidata.org/prop/direct/P17> <http://www.wikidata.org/entity/Q183> . } 
}
TT: 
@org.wikidata . entity ( ) filter contains ( occupation , " Q10800557 " ^^org.wikidata:p_occupation ( " actors " ) ) && contains ( place_of_birth , " Q183 " ^^org.wikidata:p_place_of_birth ( " germany " ) ) ;

Utterance: when did mcgee join the ncis team ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
SELECT DISTINCT ?x WHERE { 
    wd:Q838560 wdt:P4584 ?x.
}
TT:
[ first_appearance ] of @wd . fictional_character ( ) filter id == " Q838560 " ^^wd:fictional_character ;

====

Utterance: who was president when the us navy department was established ?
SPARQL: 
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/> 
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
SELECT DISTINCT ?x WHERE { 
  ?x p:P39 ?p. 
  ?p ps:P39 wd:Q11696; 
     pq:P580 ?y; 
     pq:P582 ?z. 
  wd:Q742787 wdt:P571 ?w. 
  FILTER((?y < ?w) && (?z >= ?w)) 
}
TT:
@wd . human ( ) filter contains ( position_held filter point_in_time == any ( [ inception ] of @wd . organization ( ) filter id == " Q742787 " ^^wd:organization ) , " Q11696 " ^^wd:p_position_held ) ;

====

Utterance: what state is the george washington university in ?
SPARQL: 
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
SELECT DISTINCT ?x WHERE { 
    wd:Q432637 wdt:P131+ ?x. 
    { ?x wdt:P31/wdt:P279* wd:Q475050. } UNION { ?x wdt:P31/wdt:P279* wd:Q7275. } 
}
TT:
[ located_in_the_administrative_territorial_entity : Entity ( wd:state ) ] of @wd . higher_education_institution ( ) filter id == " Q432637 " ^^wd:higher_education_institution ;

====

Utterance: what professional baseball team did michael jordan play for ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/> 
SELECT DISTINCT ?x WHERE { 
    wd:Q41421 wdt:P54 ?x. 
    ?x wdt:P641 wd:Q5369. 
}
TT:
[ member_of_sports_team filter value == any ( @wd . entity ( ) filter contains ( sport , " Q5369 " ^^wd:p_sport ) ) ] of @wd . human ( ) filter id == " Q41421 " ^^wd:human ;
====

Utterance: what year did michael jordan get drafted ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
SELECT DISTINCT ?x WHERE { 
    wd:Q41421 p:P647 ?y. 
    ?y pq:P585 ?x. 
}
TT:
[ drafted_by . point_in_time ] of @wd . human ( ) filter id == " Q41421 " ^^wd:human ;

====

Utterance: who was vp for nixon?
SPARQL: 
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/> 
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
SELECT DISTINCT ?x WHERE { 
    ?x p:P39 ?p. 
    ?p ps:P39 wd:Q11699; 
       pq:P580 ?y. 
    wd:Q9588 p:P39 ?q. 
    ?q ps:P39 wd:Q11696; 
       pq:P580 ?z. 
    FILTER(?y = ?z) 
}
TT:
@wd . entity ( ) filter contains ( position_held filter start_time == any ( [ start_time of ( position_held filter value == " Q11696 " ^^wd:p_position_held ) ] of @wd . entity ( ) filter id == " Q9588 " ^^wd:entity ) , " Q11699 " ^^wd:p_position_held ) ;

====

Utterance: who succeeded william mckinley as president ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/> 
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
SELECT DISTINCT ?x WHERE { 
    wd:Q35041 p:P39 ?p. 
    ?p ps:P39 wd:Q11696; 
       pq:P1366 ?x.  
}
TT:
[ replaced_by of ( position_held filter value == " Q11696 " ^^wd:p_position_held ) ] of @wd . human ( ) filter id == " Q35041 " ^^wd:human ;

====

Utterance: who is john cabot ' s parents ?
SPARQL: 
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
SELECT DISTINCT ?x WHERE { 
    wd:Q85642 wdt:P22|wdt:P25 ?x. 
}
TT:
[ father , mother ] of @wd . human ( ) filter id == " Q85642 " ^^wd:human ;

====

Utterance: 
when did shaq come to the nba ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/> 
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
SELECT DISTINCT ?x WHERE { 
    wd:Q169452 p:P54 ?p. 
    ?p ps:P54 ?y. 
    ?y wdt:P118 wd:Q155223. 
    ?p pq:P580 ?x. 
} ORDER BY ?x LIMIT 1
TT:
min ( start_time of [ start_time of ( member_of_sports_team filter value == any ( @wd . entity ( ) filter contains ( league , " Q155223 " ^^wd:entity ) ) ) ] of @wd . entity ( ) filter id == " Q169452 " ^^wd:entity ) ;

====

Utterance:
when did herman cain run for president ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
PREFIX p: <http://www.wikidata.org/prop/> 
SELECT DISTINCT ?x WHERE { 
    ?p wdt:P31/wdt:P279* wd:Q858439; 
       wdt:P726 wd:Q491019; 
       wdt:P585 ?x. 
}
TT:
[ point_in_time ] of @wd . entity ( ) filter instance_of == " Q858439 " ^^wd:entity_subdomain && contains ( candidate , " Q491019 " ^^wd:p_candidate ) ;

====

Utterance: 
what is mount st helens ?	
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX wikibase: <http://wikiba.se/ontology#> 
PREFIX bd: <http://www.bigdata.com/rdf#> 
PREFIX schema: <http://schema.org/> 
SELECT DISTINCT ?x WHERE { 
    SERVICE wikibase:label { bd:serviceParam wikibase:language "en". wd:Q4675 schema:description ?x. } 
}
TT:
@wd . entity ( ) filter id == " Q4675 " ^^wd:mountain ;

====

Utterance: what state is barack obama senator for ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
SELECT DISTINCT ?x WHERE { 
    wd:Q76 p:P39 ?p. 
    ?p ps:P39 wd:Q4416090. 
    ?p pq:P768 ?y. 
    ?y wdt:P131 ?x. 
}
TT:
[ < electoral_district / located_in_the_administrative_territorial_entity > of ( position_held filter value == " Q4416090 " ^^wd:p_position_held ) ] of @wd . entity ( ) filter id == " Q76 " ^^wd:entity ;

====

Utterance: who is the president of cuba in 2009?
SPARQL: 
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
SELECT DISTINCT ?x WHERE { 
    ?x p:P39 ?p. 
    ?p ps:P39 wd:Q1370482; 
       pq:P580 ?y; 
       pq:P582 ?z. 
    FILTER((?y < "2010-01-01T00:00:00Z"^^xsd:dateTime) && (?z >= "2009-01-01T00:00:00Z"^^xsd:dateTime)) 
}
TT: 
[ head_of_state filter ( point_in_time == new Date ( 2009 ) ) ] of @wd . country ( ) filter id == " Q241 " ^^wd:country ;

====

Utterance: who are the current senators from missouri ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/> 
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
SELECT DISTINCT ?x WHERE { 
    ?x p:P39 ?p. 
    ?p ps:P39 wd:Q4416090. 
    ?p pq:P768 ?y.
    ?y wdt:P131 wd:Q1581. 
    FILTER NOT EXISTS { ?p pq:P582 ?z. }
} 
TT: 
@wd . entity ( ) filter contains ( position_held filter contains ( < electoral_district / located_in_the_administrative_territorial_entity > , " Q1581 " ^^wd:p_located_in_the_administrative_territorial_entity ) , " Q4416090 " ^^wd:p_position_held ) ;

====


Utterance: who are the current senators from missouri ?
SPARQL:
SELECT DISTINCT ?x WHERE { 
    ?x <http://www.wikidata.org/prop/P39> ?p. 
    ?p <http://www.wikidata.org/prop/statement/P39> <http://www.wikidata.org/entity/Q4416090>. 
    ?p <http://www.wikidata.org/prop/qualifier/P768> ?y.  
    ?y <http://www.wikidata.org/prop/direct/P131> <http://www.wikidata.org/entity/Q1581>. 
    FILTER NOT EXISTS { ?p <http://www.wikidata.org/prop/qualifier/P582> ?z. }
}
TT: 
@wd . entity ( ) filter contains ( position_held filter contains ( < electoral_district / located_in_the_administrative_territorial_entity > , " Q1581 " ^^wd:p_located_in_the_administrative_territorial_entity ) , " Q4416090 " ^^wd:p_position_held ) ;

====


Utterance: who does sonny bill williams play for 2012 ?
SPARQL: 
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/> 
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
SELECT DISTINCT ?x WHERE { 
    wd:Q2481789 p:P54 ?p. 
    ?p ps:P54 ?x; 
       pq:P580 ?y; 
       pq:P582 ?z. 
    FILTER((?y < "2013-01-01T00:00:00Z"^^xsd:dateTime) && (?z >= "2012-01-01T00:00:00Z"^^xsd:dateTime)) 
}
TT: 
[ member_of_sports_team filter ( point_in_time == new Date ( 2012 ) ) ] of @wd . human ( ) filter id == " Q2481789 " ^^wd:human ;


====

Utterance: what books did beverly cleary right ?
SPARQL: 
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
SELECT DISTINCT ?x WHERE { 
    ?x wdt:P31/wdt:P279* wd:Q47461344; 
       wdt:P50 wd:Q1316719. 
}
TT: 
@wd . book ( ) filter contains ( author , " Q1316719 " ^^wd:p_author ) ;

====

Utterance: what character did natalie portman play in star wars ?	
SPARQL: 
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/> 
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
SELECT DISTINCT ?x WHERE { 
    ?p wdt:P8345 wd:Q462; p:P161|p:P725 ?y. 
    ?y ps:P161|ps:P725 wd:Q37876; pq:P453 ?x. 
}
TT: 
[ character_role of ( cast_member filter value == " Q37876 " ^^wd:p_cast_member ) ] of @wd . entity ( ) filter contains ( media_franchise , " Q462 " ^^wd:p_media_franchise ) ;

====

Utterance: who is the president of the european union 2011 ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/> 
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
SELECT DISTINCT ?x WHERE { 
    ?x p:P39 ?p. 
    ?p ps:P39 wd:Q740126; 
       pq:P580 ?y; 
       pq:P582 ?z. 
    FILTER((?y < "2012-01-01T00:00:00Z"^^xsd:dateTime) && (?z >= "2011-01-01T00:00:00Z"^^xsd:dateTime)) 
}
TT:
@wd . entity ( ) filter contains ( position_held filter ( point_in_time == new Date ( 2011 ) ) , " Q740126 " ^^wd:p_position_held ) ;

====

Utterance: where are the netherlands on a world map ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
SELECT DISTINCT ?x WHERE { wd:Q55 wdt:P625 ?x. }
TT:
[ location ] of @wd . entity ( ) filter id == " Q55 " ^^wd:entity ;

====
Utterance: what character did brian austin green play on 90210 ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX p: <http://www.wikidata.org/prop/> 
PREFIX ps: <http://www.wikidata.org/prop/statement/> 
PREFIX pq: <http://www.wikidata.org/prop/qualifier/> 
SELECT DISTINCT ?x WHERE { 
    wd:Q117590 p:P161 ?p. 
    ?p ps:P161 wd:Q313311; pq:P453 ?x. 
}
TT: 
[ character_role of ( cast_member filter value == " Q313311 " ^^wd:p_cast_member ) ] of @wd . television_series ( ) filter id == " Q117590 " ^^wd:television_series ;

====

Utterance: what was the name of frederick douglass book ?
SPARQL:
PREFIX wd: <http://www.wikidata.org/entity/> PREFIX wdt: <http://www.wikidata.org/prop/direct/> 
SELECT DISTINCT ?x WHERE { ?x wdt:P31/wdt:P279* wd:Q7725634; wdt:P50 wd:Q215562. }
TT:
@wd . creative_work ( ) filter contains ( author , " Q215562 " ^^wd:p_author ) && instance_of == " Q7725634 " ^^wd:creative_work_subdomain ;
